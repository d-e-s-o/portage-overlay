From 02389f41d01bc45695d136f2eeea0abc8491265c Mon Sep 17 00:00:00 2001
From: =?UTF-8?q?Daniel=20M=C3=BCller?= <deso@posteo.net>
Date: Sat, 8 Jan 2022 17:02:56 -0800
Subject: [PATCH] Support incrementing/decrementing the number below the cursor

Vim supports incrementing & decrementing the number below the cursor (or
after it) via Ctrl-a and Ctrl-x, respectively. Given fish's Vi mode
support, it makes sense to provide similar functionality when working on
the command line, to provide a more natural environment for Vim users.
With this change we add the necessary functionality.

Closes: #8320
---
 share/functions/fish_vi_key_bindings.fish |  83 ++++++++++++++++
 1 files changed, 83 insertions(+)

diff --git a/share/functions/fish_vi_key_bindings.fish b/share/functions/fish_vi_key_bindings.fish
index 83242a..b78ce0 100644
--- a/share/functions/fish_vi_key_bindings.fish
+++ b/share/functions/fish_vi_key_bindings.fish
@@ -1,3 +1,80 @@
+alias fish_vi_dec 'fish_vi_inc_dec dec'
+alias fish_vi_inc 'fish_vi_inc_dec inc'
+
+# TODO: Currently we do not support hexadecimal and octal values.
+function fish_vi_inc_dec --description 'increment or decrement the number below the cursor'
+  # The cursor is zero based, but all string functions assume 1 to be
+  # the lowest index. Adjust accordingly.
+  set --local cursor (math -- (commandline --cursor) + 1)
+  set --local line (commandline --current-buffer | string collect)
+
+  set --local candidate (string sub --start $cursor -- $line | string collect)
+  if set --local just_found (string match --regex '^-?[0-9]+' -- $candidate)
+    # Search from the current cursor position backwards for as long as we
+    # can identify a valid number.
+    set --function found $just_found
+    set --function found_at $cursor
+    set --local end (math -- $cursor + (string length -- $found) - 1)
+
+    set --local i (math -- $cursor - 1)
+    while [ $i -ge 1 ]
+      set candidate (string sub --start $i --end $end -- $line)
+      if set just_found (string match --regex '^-?[0-9]+$' -- $candidate)
+        set found $just_found
+        set found_at $i
+        # We found a candidate, but continue to make sure that we captured
+        # the complete number and not just part of it.
+      else
+        # We have already found a number earlier. Work with that.
+        break
+      end
+
+      set i (math -- $i - 1)
+    end
+  else
+    # We didn't find a match below the cursor. Mirror Vim behavior by
+    # checking ahead as well.
+    for i in (seq (math -- $cursor + 1) (math -- (string length -- $line) - 1))
+      set candidate (string sub --start $i -- $line | string collect)
+
+      if set just_found (string match --regex '^-?[0-9]+' -- $candidate)
+        set found $just_found
+        set found_at $i
+        break
+      end
+    end
+
+    if [ -z "$found" ]
+      return
+    end
+  end
+
+  if [ $argv = 'inc' ]
+    set number (math -- $found + 1)
+  else if [ $argv = 'dec' ]
+    set number (math -- $found - 1)
+  end
+
+  set --local number_abs (string trim --left --chars=- -- $number)
+  set --local signed $status
+  set --local found_abs (string trim --left --chars=- -- $found)
+  set number (string pad --char 0 --width (string length -- $found_abs) -- $number_abs)
+  if test $signed -eq 0
+    set number "-$number"
+  end
+
+  # `string sub` may bitch about `--end` being zero if `found_at` is 1.
+  # So ignore errors here...
+  set --local before (string sub --end (math -- $found_at - 1) -- $line 2> /dev/null | string collect)
+  set --local after (string sub --start (math -- $found_at + (string length -- $found)) -- $line | string collect)
+  commandline --replace -- "$before$number$after"
+  # Need to subtract two here because 1) cursor is zero based 2)
+  # `found_at` is the index of the first character of the match, but we
+  # want the one before that.
+  commandline --cursor -- (math -- $found_at + (string length -- $number) - 2)
+  commandline --function -- repaint
+end
+
 function fish_vi_key_bindings --description 'vi-like key bindings for fish'
     if contains -- -h $argv
         or contains -- --help $argv
@@ -271,6 +348,12 @@ function fish_vi_key_bindings --description 'vi-like key bindings for fish'
     bind -s --preset -M replace backspace backward-char
     bind -s --preset -M replace shift-backspace backward-char
 
+    #
+    # Increment or decrement number below the cursor with ctrl+x ctrl+a
+    #
+    bind -s --preset -M default ctrl-a fish_vi_inc
+    bind -s --preset -M default ctrl-x fish_vi_dec
+
     #
     # visual mode
     #
-- 
2.49.0

